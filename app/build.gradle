plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'com.google.android.gms.oss-licenses-plugin'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.apps.adrcotfas.goodtime"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 131
        versionName "2.2.7"
        resConfigs "ar-rSA",
                "bn-rBD",
                "bs-rBA",
                "ca-rES",
                "cs-rCZ",
                "da-rDK",
                "de-rDE",
                "el-rGR",
                "en-rUS",
                "es-rES",
                "eu-rES",
                "fi-rFI",
                "fr-rFR",
                "gl-rES",
                "hi-rIN",
                "hr-rHR",
                "hu-rHU",
                "in-rID",
                "it-rIT",
                "iw-rIL",
                "ja-rJP",
                "ko-rKR",
                "lt-rLT",
                "lv-rLV",
                "nl-rNL",
                "no-rNO",
                "pl-rPL",
                "pt-rBR",
                "pt-rPT",
                "ro-rRO",
                "ru-rRU",
                "sk-rSK",
                "sl-rSI",
                "sr-rCS",
                "sr-rSP",
                "sv-rSE",
                "th-rTH",
                "tr-rTR",
                "uk-rUA",
                "vi-rVN",
                "zh-rCN",
                "zh-rTW"
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions("cevafin")
    productFlavors {
        google {
            dimension "cevafin"
            buildConfigField "boolean", "F_DROID", "false"
        }
        fdroid {
            dimension "cevafin"
            buildConfigField "boolean", "F_DROID", "true"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    // to use the latest java features (needed java.time)
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"

    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    kapt 'androidx.databinding:databinding-compiler:7.0.2'

    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.room:room-ktx:2.3.0'
    kapt "androidx.room:room-compiler:2.3.0"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Color picker for the labels
    implementation 'com.takisoft.preferencex:preferencex-colorpicker:1.1.0'

    // EventBus
    implementation 'org.greenrobot:eventbus:3.2.0'

    // Material components
    implementation 'com.google.android.material:material:1.4.0'

    // About
    //implementation 'com.github.daniel-stoneuk:material-about-library:x.y.z'
    implementation 'com.github.adrcotfas:material-about-library:a65545f360'

    // Statistics
    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'

    // RecyclerView selection framework
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'

    // Icons
    implementation 'com.mikepenz:iconics-core:3.2.5'
    implementation 'com.mikepenz:community-material-typeface:2.7.94.1@aar'

    // Intro
    implementation 'com.heinrichreimersoftware:material-intro:2.0.0'

    // App rating
    implementation 'io.github.kobakei:ratethisapp:1.2.0'

    // In-app purchases
    googleImplementation "com.android.billingclient:billing-ktx:4.0.0"

    implementation 'xyz.aprildown:UltimateRingtonePicker:3.1.0'

    // open source licences list
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'com.google.dagger:hilt-android:2.38.1'
    kapt 'com.google.dagger:hilt-compiler:2.38.1'
}

kapt {
    correctErrorTypes true
}
